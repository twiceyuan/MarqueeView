apply plugin: 'com.android.library'
apply plugin: 'maven'

android {
    compileSdkVersion 26

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        resourcePrefix "marqueeView_"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}


// build a jar with source files
// 指定编码
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    failOnError  false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}
// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    //noinspection GroovyAccessibility
    from javadoc.destinationDir
}
artifacts {
    archives sourcesJar
    archives javadocJar
}

final VERSION_NAME = '0.1'
final GROUP_ID = 'com.twiceyuan'
final MODULE_ID = 'marqueeview'

uploadArchives {
    repositories.mavenDeployer {
        repository(url: REPO_URL) { // 仓库地址
            // 这里配置上传账户的用户名和密码
            authentication(userName: REPO_USERNAME, password: REPO_PASSWORD)
        }
        pom.version = VERSION_NAME
        pom.groupId = GROUP_ID
        pom.artifactId = MODULE_ID
    }
}